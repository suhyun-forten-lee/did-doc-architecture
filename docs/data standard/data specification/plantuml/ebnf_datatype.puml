@startebnf type_uuid
uuid = 4*hex-octet, "-", 2*hex-octet, "-", 2*hex-octet, "-", 2*hex-octet, "-", 6*hex-octet;
hex-octet = HEXDIG, HEXDIG;
@endebnf

@startebnf type_utc_datetime
utc-datetime   = full-date, "T", full-time;
full-date      = date-fullyear, "-", date-month, "-", date-mday;
full-time      = partial-time, time-offset;

partial-time   = time-hour, ":", time-minute, ":", time-second, [time-secfrac];
time-offset    = "Z" | time-numoffset;

time-numoffset = ("+" | "-"), time-hour, ":", time-minute;

(* 충분히 의미 전달이 가능하므로 이하 date-fullyear 등은 생략 *)
@endebnf

@startebnf type_multibase
multibase = "f", ? lower case hexstring ?
          | "F", ? upper case hexstring ?
          | "z", ? base58 string ?
          | "m", ? base64 string ?
          | "u", ? base64url string ?
          ;
@endebnf

@startebnf type_namespace_id
namespace-id = " " | identifier, {".", identifier};
@endebnf


@startebnf type_did
did                = "did", ":", method-name, ":", method-specific-id;
method-name        = identifier;
method-specific-id = ? defined by the method specification ?;
@endebnf

@startebnf type_did_url
did-url = did, {"/", segment}, ["?", query-list], ["#", fragment];
@endebnf

@startebnf type_did_version_id
did-version-id = {DIGIT}-;
@endebnf

@startebnf type_did_key_id
did-key-id = item-name;
@endebnf

@startebnf type_did_service_id
did-service-id = item-name;
@endebnf

@startebnf type_did_key_url
did-key-url = did, "?", "versionId", "=", did-version-id, "#", did-key-id;
@endebnf

@startebnf type_did_service_url
did-service-url = did, "?", "versionId", "=", did-version-id, "#", did-service-id;
@endebnf

@startebnf type_message_id
message-id    = yyyymmdd, hhmmss, micro-secfrac, short-random;
yyyymmdd      = date-fullyear, date-month, date-mday;
hhmmss        = time-hour, time-minute, time-second ;
micro-secfrac = 6*DIGIT;
short-random  = 8*HEXDIG;
@endebnf